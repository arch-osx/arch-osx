# PKGBUILD autocreated by ABStoOSX 0.2
# $Id
# ArchLinux Maintainer: kevin <kevin@archlinux.org>
# TU: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>
 
pkgname=boost
pkgver=1.37.0
_boostver=$(echo $pkgver | sed 's/\./_/g')
pkgrel=2
pkgdesc="Boost provides free peer-reviewed portable C++ source libraries."
arch=('macx86')
url="http://www.boost.org/"
depends=('python>=2.6' 'bzip2' 'zlib')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}_${_boostver}.tar.gz)
license=('custom')
 
build() {
  cd ${srcdir}/${pkgname}_${_boostver}
  export CFLAGS="${CFLAGS} -fno-strict-aliasing"

  # build bjam
  cd ${srcdir}/${pkgname}_${_boostver}/tools/jam/src
  ./build.sh cc || return 1

  _bindir="bin.macosxx86"

  install -m755 -d ${pkgdir}/opt/arch/bin
  install -m755 ${_bindir}/bjam ${pkgdir}/opt/arch/bin/bjam || return 1
 
  # build bcp
  cd ${srcdir}/${pkgname}_${_boostver}/tools/bcp
  ../jam/src/${_bindir}/bjam toolset=darwin || return 1
  install -m755 ${srcdir}/${pkgname}_${_boostver}/dist/bin/bcp \
                ${pkgdir}/opt/arch/bin/bcp || return 1

  # build libs
  cd ${srcdir}/${pkgname}_${_boostver}
  # default "debug release <runtime-link>static/dynamic <threading>single/multi"
  # --layout=system removes the -gcc suffix from libraries and installs
  # includes in /opt/arch/include/boost.
  ./tools/jam/src/${_bindir}/bjam toolset=darwin \
            release debug-symbols=off threading=single,multi \
			runtime-link=shared link=shared,static \
            --prefix=${pkgdir}/opt/arch \
            -sPYTHON_ROOT=/opt/arch \
            -sPYTHON_VERSION=2.6 \
            -sTOOLS=gcc \
            --layout=system \
            install || return 1
  # Change the install name of the libraries to allow easier linking
  cd ${pkgdir}/opt/arch/lib
  for i in *.dylib; do
    install_name_tool -id /opt/arch/lib/$i $i
  done
  # build pyste
  cd ${srcdir}/${pkgname}_${_boostver}/libs/python/pyste/install
  python setup.py install --root=${pkgdir} || return 1

  # license
  install -m755 -d ${pkgdir}/opt/arch/share/licenses/${pkgname}
  install -m644 ${srcdir}/${pkgname}_${_boostver}/LICENSE_1_0.txt \
                ${pkgdir}/opt/arch/share/licenses/${pkgname}/ || return 1
}
md5sums=('c38b88bb4ebc6d0d9193f432842273d2'
         'd7f821056540ef08eb1d5ebd5ed017f1')
