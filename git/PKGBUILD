# PKGBUILD autocreated by ABStoOSX 0.2
# $Id
# ArchLinux Maintainer: Dan McGee <dan@archlinux.org>

pkgname=git
pkgver=1.6.4
pkgrel=1
pkgdesc="GIT - the stupid content tracker"
arch=('macx86')
url="http://git.or.cz/"
license=('GPL2')
depends=('curl' 'openssl' 'expat>=2.0' 'perl-error' 'perl>=5.10.0')
optdepends=('tk: gitk and git gui'
            'perl-libwww: git svn'
            'perl-term-readkey: git svn'
            'subversion: git svn'
            'cvsps: git cvsimport')
replaces=('git-core')
provides=('git-core')
source=("http://kernel.org/pub/software/scm/git/${pkgname}-${pkgver}.tar.bz2" \
        "http://kernel.org/pub/software/scm/git/git-manpages-${pkgver}.tar.bz2")

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/opt/arch --with-openssl=/opt/arch --with-curl=/opt/arch --with-expat=/opt/arch --with-perl=/opt/arch/bin/perl
  make prefix=/opt/arch gitexecdir=/opt/arch/lib/git-core || return 1
  make prefix=/opt/arch gitexecdir=/opt/arch/lib/git-core \
    INSTALLDIRS=vendor DESTDIR=${pkgdir} install || return 1
  
  chown -R root.0 ${pkgdir}
  # let's plop gitweb in /opt/arch/share
  mkdir -p $pkgdir/opt/arch/share/
  cp -dR ./gitweb $pkgdir/opt/arch/share/gitweb || return 1

  #bash completion
  mkdir -p $pkgdir/opt/arch/etc/bash_completion.d/
  install -m644 ./contrib/completion/git-completion.bash $pkgdir/opt/arch/etc/bash_completion.d/git || return 1

  # how 'bout some manpages?
  for mansect in man1 man5 man7; do
    for manpage in $srcdir/$mansect/*; do
      install -D -m644 $manpage $pkgdir/opt/arch/share/man/$mansect/$(basename $manpage)
    done
  done

  # remove perllocal.pod, .packlist, and empty directories.
  rm -rf $pkgdir/opt/arch/lib/perl5
}

md5sums=('d41e3d43c8f1d23024fb3d3fe6aa3858'
         '851e1df833895c5046a994c28d3d8368')
sha256sums=('31d1346211df985288a21426bc3e820be817ead589ae32c7e545854480bc8185'
            'b7ed025920594dff4ab9dc959bbefd99fa4ce3ebadef81fc92aad4d66065b7d5')
