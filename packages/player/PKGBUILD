# This is a PKGBUILD for Player from the Player/Stage project
# ArchOSX Maintainer: Kevin Barry <barryk gmail com>

pkgname=player
pkgver=2.1.2
pkgrel=2
pkgdesc="Robotic systems environment"
arch=('macx86')
url="http://playerstage.sf.net"
license=('GPL')
groups=('playerstage')
depends=('gtk2' 'gsl' 'opencv' 'libdc1394' 'zlib' 'libtool')
makedepends=('python' 'boost' 'libgnomecanvas')
optdepends=('libgnomecanvas: playernav navigation tool'
            'boost: libplayerc++')
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
source=(http://dl.sourceforge.net/sourceforge/playerstage/player-${pkgver}.tar.bz2
        'playerc++boost.pc')
md5sums=('324cb05252c916d4ce6106d6c83b6131'
         'df7ee6f9d0d19d3d3ca2503269922cda')

build() {
  cd "$startdir/src/$pkgname-$pkgver"
  
  find . -name Makefile.in -exec \
    sed -e 's!^LIBS = !LIBS = -L/opt/arch/lib!g'\
        -e 's!^PLAYER_DRIVER_EXTRA_LIBS =!PLAYER_DRIVER_EXTRA_LIBS = -L/opt/arch/lib!g' -i~ {} \;
  # Build the main distribution
  ./configure --prefix=/opt/arch --without-boost-signals --without-boost-thread
  cd libplayerxdr && make && cd ..
  cd libplayercore && make && cd ..
  make -j1 || return 1
  make DESTDIR="$startdir/pkg" install

  # Build the boost libplayerc++
  ./configure --prefix=/opt/arch --with-boost-signals --with-boost-thread
  cd client_libs/libplayerc++ || return 1
  make clean
  rm -rf .libs
  make -j1 || return 1
  install_name_tool -id /opt/arch/lib/libplayerc++boost.${pkgver}.dylib .libs/libplayerc++.${pkgver}.dylib
  install -m755 .libs/libplayerc++.${pkgver}.dylib ${pkgdir}/opt/arch/lib/libplayerc++boost.${pkgver}.dylib
  install -m755 ${startdir}/playerc++boost.pc ${pkgdir}/opt/arch/lib/pkgconfig/playerc++boost.pc
  ln -s libplayerc++boost.${pkgver}.dylib ${pkgdir}/opt/arch/lib/libplayerc++boost.dylib
  ln -s libplayerc++boost.${pkgver}.dylib ${pkgdir}/opt/arch/lib/libplayerc++boost.2.dylib

  
}

# vim:set ts=2 sw=2 et:

