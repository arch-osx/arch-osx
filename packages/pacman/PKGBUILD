# $Id: PKGBUILD 23245 2009-01-07 02:10:32Z dan $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=pacman
pkgver=3.2.2
pkgrel=5
pkgdesc="A library-based package manager with dependency support"
arch=('macx86')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('libarchive>=2.6.0' 'libfetch>=2.2' 'pacman-mirrorlist' 'gettext')
optdepends=('fakeroot: for makepkg usage as normal user'
            'python: for rankmirrors script usage'
            'coreutils: for repo-add')
backup=(opt/arch/etc/pacman.conf opt/arch/etc/makepkg.conf opt/arch/etc/pacman.d/mirrorlist)
install=pacman.install
options=(!libtool)
source=(ftp://ftp.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz
        pacman.conf
        makepkg.conf
        repo_add_sedspaces.patch
            # Patch repo-add to work with BSD/sed or GNU/sed
        stat_full_path.patch
            # Patch to use full path of stat in case user has coreutils
        makepkg_sed.patch 
            # Patch makepkg to work with BSD/sed or GNU/sed
        )
md5sums=('8404fc38566bb52f8eee2ea483a41051')

build() {
  cd $srcdir/$pkgname-$pkgver
  
  patch -p0 < $startdir/stat_full_path.patch
  patch -p0 < $startdir/repo_add_sedspaces.patch
  patch -p0 < $startdir/makepkg_sed.patch
  sed -i~ -e 's/--strip-debug/-S/' scripts/makepkg.sh.in # Work with Mac's strip
  ./configure --prefix=/opt/arch
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  # install arch specific stuff
  mkdir -p $pkgdir/opt/arch/etc
  install -m644 $srcdir/pacman.conf $pkgdir/opt/arch/etc/
  install -m644 $srcdir/makepkg.conf $pkgdir/opt/arch/etc/

  # install completion files
  mkdir -p $pkgdir/opt/arch/etc/bash_completion.d/
  install -m644 contrib/bash_completion $pkgdir/opt/arch/etc/bash_completion.d/pacman
  mkdir -p $pkgdir/opt/arch/share/zsh/site-functions/
  install -m644 contrib/zsh_completion $pkgdir/opt/arch/share/zsh/site-functions/_pacman
}

# vim: set ts=2 sw=2 et:
