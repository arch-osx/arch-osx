# ArchLinux Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=ffmpeg
pkgver=0.5
pkgrel=1
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=('macx86')
url="http://ffmpeg.mplayerhq.hu/"
license=('LGPL')
depends=('lame' 'sdl' 'libvorbis' 'faad2>=2.7' 'faac' 'xvidcore' 'zlib' 'imlib2' 'x264>=20090216' 'libtheora')
source=(http://ffmpeg.mplayerhq.hu/releases/${pkgname}-${pkgver}.tar.bz2)

build() {
  cd "$srcdir/${pkgname}-${pkgver}" || return 1

  ./configure \
  --prefix=/opt/arch \
  --enable-gpl \
  --enable-libmp3lame \
  --enable-libvorbis \
  --enable-libfaac \
  --enable-libfaad \
  --enable-libxvid \
  --enable-libx264 \
  --enable-libtheora \
  --enable-postproc \
  --enable-shared \
  --enable-pthreads \
  --enable-x11grab \
  --enable-swscale \
  || return 1

  make || return 1
  make doc/ff{mpeg,play,server}.1 || return 1

  make DESTDIR="$pkgdir" install install-man || return 1

  # since makepkg currently declines to strip .a files, do this for now
  strip -S $startdir/pkg/opt/arch/lib/*.a || return 1
}

# vim:set ts=2 sw=2 et:
