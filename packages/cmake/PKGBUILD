# ArchOSX Maintainer: Kevin Barry <barryk gmail com>
# ArchLinux Maintainer: Pierre Schmitz <pierre archlinux.de>
# Contributor: damir <damir archlinux.org>
# Contributor: Konstantinos Pachnis <kpachnis mes-direct.com>

pkgname=cmake
pkgver=2.6.2
pkgrel=3
pkgdesc="CMake is a cross-platform open-source make system"
arch=('macx86')
license=('custom')
url="http://www.cmake.org"
depends=('expat' 'xmlrpc-c' 'gettext' 'curl')
source=("http://www.cmake.org/files/v2.6/$pkgname-$pkgver.tar.gz")
md5sums=('9e82aa3beb991aa8e5797cf330618d42')

build() {
    cd $srcdir/$pkgname-$pkgver
    ./bootstrap --prefix=/opt/arch --system-libs
    sed -e 's^/usr/lib/libz.dylib^/opt/arch/lib/libz.dylib^g' \
        -e 's^/usr/lib/libcurl.dylib^/opt/arch/lib/libcurl.dylib^g' \
        -e 's^/usr/lib/libexpat.dylib^/opt/arch/lib/libexpat.dylib^g' \
        -e 's^/usr/lib/libcurses.dylib^/opt/arch/lib/libncurses.dylib^g' \
        -i~ CMakeCache.txt
    ./bootstrap --prefix=/opt/arch --system-libs
    make || return 1
    make DESTDIR=$pkgdir install
    
    mkdir -p $pkgdir/opt/arch/share/vim/indent
    mkdir -p $pkgdir/opt/arch/share/vim/syntax
    mkdir -p $pkgdir/opt/arch/share/emacs/site-lisp
    mkdir -p $pkgdir/opt/arch/share/licenses/$pkgname

    mv $pkgdir/opt/arch/man $pkgdir/opt/arch/share/man

    install -m644 Docs/cmake-indent.vim $pkgdir/opt/arch/share/vim/indent/cmake-indent.vim
    install -m644 Docs/cmake-syntax.vim $pkgdir/opt/arch/share/vim/syntax/cmake-syntax.vim
    install -m644 Docs/cmake-mode.el $pkgdir/opt/arch/share/emacs/site-lisp/cmake-mode.el
    install -m644 Copyright.txt $pkgdir/opt/arch/share/licenses/$pkgname/Copyright.txt    
}
