# PKGBUILD autocreated by ABStoOSX 0.2
# $Id
# ArchLinux Maintainer: judd <jvinet@zeroflux.org>
pkgname=perl
pkgver=5.10.0
pkgrel=4
pkgdesc="Practical Extraction and Report Language"
arch=('macx86')
license=('GPL' 'PerlArtistic')
url="http://www.perl.org"
groups=('base')
#depends=('gdbm' 'db>=4.7' 'coreutils')
depends=('coreutils')
# for site_perl this is needed!
#test without options=('emptydirs')
source=(http://www.perl.com/CPAN/src/perl-${pkgver}.tar.gz
    perlbin.sh perl-5.8.6-picdl.patch0
    perl-5.10.0-archlinux-inc-order.patch)
#install=perl.install
provides=(
# extra
perl-archive-tar perl-compress-raw-zlib perl-compress-zlib
perl-extutils-cbuilder perl-io-compress-base perl-io-compress-zlib perl-io-zlib
perl-module-pluggable perl-pod-escapes perl-pod-simple perl-module-build
perl-version

# aur
perl-archive-extract perl-cpanplus perl-digest-sha perl-extutils-parsexs
perl-file-fetch perl-ipc-cmd perl-locale-maketext-simple perl-log-message
perl-log-message-simple perl-module-corelist perl-module-load
perl-module-load-conditional perl-module-loaded perl-module-pluggable
perl-object-accessor perl-params-check perl-term-ui perl-time-piece
)
md5sums=('d2c39b002ebfd2c3c5dba589365c5a71'
         '0a623d8ee22319d3a1daa4497fa96982'
         '319b56a7ce715fb7a494fe4d5cb9474c'
         '0e4853bd75ee36b8f4c70b78c707b2fa')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}

  # Update some modules provided with perl:
  # replace the old DB_File with the newest one from CPAN,
  # since it supports sleepycat's db 4.1.x
#  cd ext
#  rm -rf DB_File
#  mv $startdir/src/DB_File-1.814 ./DB_File

  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -Np0 -i ../perl-5.10.0-archlinux-inc-order.patch

  if [ "${CARCH}" = "x86_64" ]; then
    # for x86_64
    arch_opts="-Dcccdlflags='-fPIC'"
    patch -Np0 -i ../perl-5.8.6-picdl.patch0 || return 1
  else 
    # for i686
    arch_opts=""
  fi
  ./Configure -des -Dusethreads -Doptimize="${CFLAGS}" -Dprefix=/opt/arch \
    -Dinstallprefix=${startdir}/pkg/opt/arch -Dvendorprefix=/opt/arch \
    -Dprivlib=/opt/arch/share/perl5/core_perl \
    -Darchlib=/opt/arch/lib/perl5/core_perl \
    -Dsitelib=/opt/arch/share/perl5/site_perl/${pkgver} \
    -Dsitearch=/opt/arch/lib/perl5/site_perl/${pkgver} \
    -Dvendorlib=/opt/arch/share/perl5/vendor_perl \
    -Dvendorarch=/opt/arch/lib/perl5/vendor_perl \
    -Dotherlibdirs=/opt/arch/lib/perl5/current:/opt/arch/lib/perl5/site_perl/current \
    -Dscriptdir='/opt/arch/bin/perlbin/core' \
    -Dsitescript='/opt/arch/bin/perlbin/site' \
    -Dvendorscript='/opt/arch/bin/perlbin/vendor' \
    -Dman1ext=1perl -Dman3ext=3perl ${arch_opts}

  make || return 1
  make install

  ### Perl Settings ###
  # Change man page extensions for site and vendor module builds.
  sed -e '/^man1ext=/ s/1perl/1p/' -e '/^man3ext=/ s/3perl/3pm/' \
      -i~ ${startdir}/pkg/opt/arch/lib/perl5/core_perl/Config_heavy.pl

  # Tell perl scripts to start with env perl rather than a full path
  sed -e "s^startperl='#!.*^startperl='#!/usr/bin/env perl'^" \
      -i~ ${startdir}/pkg/opt/arch/lib/perl5/core_perl/Config_heavy.pl

  ### CPAN Settings ###
  # Set CPAN default config to use the site directories.
  sed -e '/(makepl_arg =>/   s/""/"INSTALLDIRS=site"/' \
      -e '/(mbuildpl_arg =>/ s/""/"installdirs=site"/' \
      -i~ ${startdir}/pkg/opt/arch/share/perl5/core_perl/CPAN/FirstTime.pm

  ### CPANPLUS Settings ###
  # Set CPANPLUS default config to use the site directories.
  sed -e "/{'makemakerflags'}/ s/'';/'INSTALLDIRS=site';/" \
      -e "/{'buildflags'}/     s/'';/'installdirs=site';/" \
      -i~ ${startdir}/pkg/opt/arch/share/perl5/core_perl/CPANPLUS/Config.pm

  # Profile script so set paths to perl scripts.
  mkdir -p ${startdir}/pkg/opt/arch/etc/profile.d
  install -m755 ${startdir}/src/perlbin.sh \
                   ${startdir}/pkg/opt/arch/etc/profile.d/perlbin.sh

  (cd ${startdir}/pkg/opt/arch/bin; mv perl${pkgver} perl)
  (cd ${startdir}/pkg/opt/arch/bin/perlbin/core; \
      ln -sf c2ph pstruct; ln -sf s2p psed)
  grep -Rl "${startdir}/pkg" ${startdir}/pkg/opt/arch | \
      xargs sed -i~ "s^${startdir}/pkg^^g"
  find ${startdir}/pkg -name \*~ -delete
}
