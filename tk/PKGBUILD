# PKGBUILD autocreated by ABStoOSX 0.2
# $Id
# ArchLinux Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tk
pkgver=8.5.6
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl"
arch=('macx86')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}")
source=(http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz
        'tcl_include_path.patch')
md5sums=('7da2e00adddc7eed6080df904579d94e'
         'a7c039e7252cd6d4e7c9832f18e0defd')

build() {
  cd ${srcdir}/tk${pkgver}/unix  
  if [ "$CARCH" = "x86_64" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-threads --enable-64bit
  elif [  "$CARCH" = "macx86" ]; then
    patch -p1 < $startdir/tcl_include_path.patch
    ./configure --prefix=/opt/arch --mandir=/opt/arch/share/man --enable-threads --disable-64bit --enable-aqua --with-tcl=/opt/arch/lib
  else
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-threads --disable-64bit
  fi
  
  make
  make INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf wish8.5 ${pkgdir}/opt/arch/bin/wish
  # install license
  mkdir -p ${pkgdir}/opt/arch/share/licenses/${pkgname}
  install -m644 ../license.terms ${pkgdir}/opt/arch/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces / fixes #3602
  sed -i~ \
  -e "s,^TK_BUILD_LIB_SPEC='-L.*/unix,TK_BUILD_LIB_SPEC='-L/usr/lib," \
  -e "s,^TK_SRC_DIR='.*',TK_SRC_DIR='/usr/include'," \
  -e "s,^TK_BUILD_STUB_LIB_SPEC='-L.*/unix,TK_BUILD_STUB_LIB_SPEC='-L/usr/lib," \
  -e "s,^TK_BUILD_STUB_LIB_PATH='.*/unix,TK_BUILD_STUB_LIB_PATH='/usr/lib," \
  ${pkgdir}/opt/arch/lib/tkConfig.sh

  # Remove TK's custom X11 includes...
  rm -rf ${pkgdir}/opt/arch/include/X11
}
