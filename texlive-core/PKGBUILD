# PKGBUILD autocreated by ABStoOSX 0.2
# $Id
# ArchLinux Maintainer: Firmicus <firmicus ατ gmx δοτ net>
# Pulled ArchLinux SVN rev 18426 rather than current. No binary available for current.

pkgname=texlive-core
pkgver=2008.11171
pkgrel=1
pkgdesc="TeX Live core distribution"
license=('GPL')
arch=('macx86')
depends=('texlive-bin')
optdepends=('perl' 'ruby' 'ghostscript' 'psutils')
makedepends=('lzma-utils')
groups=('texlive-most')
conflicts=('tetex')
provides=('tetex')
replaces=('tetex')
url='http://tug.org/texlive/'
source=( #"http://dev.archlinux.org/~francois/texlive/src/$pkgname-src.zip"
        "http://ftp.uni-kl.de/pub/linux/archlinux/other/texlive/${pkgname}-src.zip"
        'texlive.install')
install=texlive.install
backup=(/opt/arch/share/texmf-config/tex/context/config/cont-usr.tex)
    
md5sums=('c54ab6f354d83633ead89b255099371c'
         '78d8b10ed676f36f77bb5adf684b89ba')

build() {
  cd $srcdir
   install -m755 -d $pkgdir/opt/arch/share/texmf-var/arch/installedpkgs || return 1
   sed -i~ '/^#/d' CONTENTS || return 1
   _revnr=`echo $pkgver | sed 's/2008\.//'`
   install -m644 CONTENTS $pkgdir/opt/arch/share/texmf-var/arch/installedpkgs/${pkgname}_${_revnr}.pkgs || return 1
   for p in *.tar.lzma; do 
	   lzma --force -dc $p | tar xf - || return 1
   done
   install -m755 -d $pkgdir/opt/arch/share || return 1
   find texmf-dist -type d -exec install -d -m755 $pkgdir/opt/arch/share/'{}' \; || return 1
   find texmf-dist -type f -exec install -m644 '{}' $pkgdir/opt/arch/share/'{}' \; || return 1
   find $pkgdir/opt/arch/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \; || return 1
   # copy config file to texmf-config
   mkdir -p $pkgdir/opt/arch/share/texmf-config/tex/context/config
   cp -a $pkgdir/opt/arch/share/texmf-dist/tex/context/config/cont-usr.tex \
   	$pkgdir/opt/arch/share/texmf-config/tex/context/config/cont-usr.tex
  
  # remove obsolete stuff (we don't provide the omega and aleph binaries anyway)
  rm -rf $pkgdir/opt/arch/share/texmf-dist/omega
  rm -f $pkgdir/opt/arch/share/texmf-dist/tex/plain/config/{omega,aleph}.ini
  # more cleanup
  rm -rf $pkgdir/opt/arch/share/texmf-dist/scripts/context/stubs/mswin/
  # remove doc and sources for koma-script
  rm -rf $pkgdir/opt/arch/share/texmf-dist/doc/
  rm -rf $pkgdir/opt/arch/share/texmf-dist/source/
}
