# ArchOSX Maintainer: Kevin Barry <barryk gmail com>
# ArchLinux Maintainer: Eric Belanger <eric archlinux.org>

# NOTE: ImageMagick builds against an existing installation
# uninstall ImageMagick before building, or build it, install it, build it.

# NOTE 2: To circumvent linking problems (FS#10574), this package must now be built the following way:
# install old package, build new package, install new package, rebuild

pkgname=imagemagick
pkgver=6.4.9.2
pkgrel=1
pkgdesc="An image viewing/manipulation program"
arch=('macx86')
url="http://www.imagemagick.org/"
license=('custom')
depends=('bzip2' 'libxml2')
makedepends=('perl')
options=('!makeflags' '!docs')
source=(ftp://ftp.imagemagick.org/pub/ImageMagick/ImageMagick-${pkgver%.*}-${pkgver##*.}.tar.bz2 \
        libpng_mmx_patch_x86_64.patch add_delegate.patch)

build() {
  cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}

  if [ "${CARCH}" = "x86_64" ]; then
    patch -Np1 < ../libpng_mmx_patch_x86_64.patch || return 1
  fi

  patch -p0 < ../add_delegate.patch || return 1
  sed -e "s/with_autotrace='no'/with_autotrace='yes'/" -i~ configure || return 1

  ./configure --prefix=/opt/arch --without-modules --disable-static --enable-openmp \
              --with-x --with-wmf --with-openexr --with-xml \
              --with-gslib --with-gs-font-dir=/usr/share/fonts/Type1 \
              --with-perl --with-perl-options="INSTALLDIRS=vendor" \
              --without-gvc --without-djvu --with-jp2 \
              --without-jbig --without-fpx --without-dps || return 1

  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
  install -D -m644 NOTICE ${pkgdir}/usr/share/licenses/${pkgname}/NOTICE || return 1

  #Cleaning
  find ${pkgdir} -name '*.bs' -exec rm {} \; || return 1
  find ${pkgdir} -name '.packlist' -exec rm {} \; || return 1
  find ${pkgdir} -name 'perllocal.pod' -exec rm {} \; || return 1

  rm -f ${pkgdir}/usr/lib/*.la || return 1
}
