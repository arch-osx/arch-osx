# ArchOSX Maintainer: Kevin Barry <barryk gmail com>
# ArchLinux Maintainer: Jan de Groot <jgc archlinux.org>

pkgname=glib2
pkgver=2.18.4
pkgrel=1
pkgdesc="Common C routines used by GTK+ 2.4 and other libs"
url="http://www.gtk.org/"
arch=(macx86)
license=('LGPL')
depends=('pcre>=7.8' 'gettext')
makedepends=('pkgconfig')
options=('!libtool' '!docs')
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/2.18/glib-${pkgver}.tar.bz2
    glib2.sh
    glib2.csh)
md5sums=('ec25ed261534d870141000ab73f1a2bf'
         '803017b365bd35dc20b092ce43b8c8c5'
         '90c7b830bef4baf225c2eb8b7ead0cab')

build() {
  cd "${srcdir}/glib-${pkgver}"
  ./configure --prefix=/opt/arch --enable-static --enable-shared --with-pcre=system --disable-fam || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  
  sed -e 's^#!/usr/bin/perl^#!/usr/bin/env perl^' -i~ ${pkgdir}/opt/arch/bin/glib-mkenums &&
    rm -f ${pkgdir}/opt/arch/bin/glib-mkenums~

  install -d "${pkgdir}/opt/arch/etc/profile.d"
  install -m755 "${srcdir}/glib2.sh" "${pkgdir}/opt/arch/etc/profile.d/" || return 1
  install -m755 "${srcdir}/glib2.csh" "${pkgdir}/opt/arch/etc/profile.d/" || return 1
  rm -f ${pkgdir}/opt/arch/lib/charset.alias # from libiconv
}

